plugins {
    id "com.diffplug.spotless" version "6.19.0"
    id "org.graalvm.buildtools.native" version "0.11.0" // or dependsOn fails in graalvm examples
    id "com.asarkar.gradle.build-time-tracker" version "4.3.0"
    id "org.jetbrains.kotlin.jvm" version "1.9.25" apply false
    id "org.jetbrains.kotlin.kapt" version "1.9.25" apply false
}

group = groupId
version = koraVersion

repositories {
    mavenLocal()
    mavenCentral()
    maven { url "https://central.sonatype.com/repository/maven-snapshots" }
}

subprojects {
    if (!project.childProjects.isEmpty()) {
        return
    }

    apply plugin: "java"
    apply plugin: "com.asarkar.gradle.build-time-tracker"

    if (!project.name.contains("kotlin")) {
        apply plugin: "com.diffplug.spotless"

        spotless {
            java {
                encoding("UTF-8")
                importOrder()
                removeUnusedImports()
                eclipse("4.21").configFile("${rootDir}/config/codestyle.xml")
                targetExclude("**/proto/**", "**/generated/openapi/**", "**/generated/soap/**", "**/generated/grpc/**",)
            }
        }
    }

    group = groupId
    version = koraVersion

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://central.sonatype.com/repository/maven-snapshots" }
    }

    configurations {
        configureEach {
            resolutionStrategy {
                cacheChangingModulesFor 0, "seconds" // check for updates every build
            }
        }
    }

    task allDeps(type: DependencyReportTask) {}
}

apply from: "${project.rootDir}/grouping.gradle"
