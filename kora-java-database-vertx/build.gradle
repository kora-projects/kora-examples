plugins {
    id "java"
    id "jacoco"
    id "application"

    id "com.github.johnrengelman.shadow" version "8.1.1"
}

repositories {
    mavenLocal()
    mavenCentral()
}

mainClassName = "ru.tinkoff.kora.example.vertx.Application"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

configurations {
    koraBom
    implementation.extendsFrom(koraBom)
    annotationProcessor.extendsFrom(koraBom)
}

dependencies {
    koraBom platform("ru.tinkoff.kora:kora-parent:$koraVersion")
    annotationProcessor "ru.tinkoff.kora:annotation-processors"
    annotationProcessor "org.slf4j:slf4j-simple:2.0.11"

    implementation "ru.tinkoff.kora:database-vertx"
    implementation "io.vertx:vertx-pg-client:4.3.8"
    implementation "com.ongres.scram:client:2.1"
    implementation "io.projectreactor:reactor-core:3.5.10" // For reactive examples (optional)

    implementation "ru.tinkoff.kora:logging-logback"
    implementation "ru.tinkoff.kora:config-hocon"

    testImplementation "ru.tinkoff.kora:test-junit5"
    testImplementation "io.goodforgod:testcontainers-extensions-postgres:0.9.6"
    testImplementation "org.postgresql:postgresql:42.6.0"
}

//noinspection GroovyAssignabilityCheck
run {
    environment([
            "POSTGRES_VERTX_URI": "postgresql://$postgresHost:$postgresPort/$postgresDatabase",
            "POSTGRES_USER"     : "$postgresUser",
            "POSTGRES_PASS"     : "$postgresPassword",
    ])
}

test {
    jvmArgs += [
            "-XX:+TieredCompilation",
            "-XX:TieredStopAtLevel=1",
    ]

    environment([
            "": ""
    ])

    useJUnitPlatform()
    testLogging {
        showStandardStreams(true)
        events("passed", "skipped", "failed")
        exceptionFormat("full")
    }

    jacoco {
        excludes += ["**/Application*"]
    }

    reports {
        html.required = false
        junitXml.required = false
    }
}

jar.enabled = true
shadowJar {
    mergeServiceFiles()
    manifest {
        attributes "Main-Class": mainClassName
        attributes "Implementation-Version": koraVersion
        attributes "Build-Time": java.time.OffsetDateTime.now()
    }
}


artifacts {
    archives shadowJar
}

compileJava {
    options.encoding("UTF-8")
    options.incremental(true)
    options.fork = true
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.required = true
        html.outputLocation = layout.buildDirectory.dir("jacocoHtml")
    }
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}