plugins {
    id "java"
    id "jacoco"
    id "application"

    id "com.google.protobuf" version "0.9.4"
    id "com.github.johnrengelman.shadow" version "8.1.1"
}

repositories {
    mavenLocal()
    mavenCentral()
}

mainClassName = "ru.tinkoff.kora.example.grpc.server.Application"

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

configurations {
    koraBom
    implementation.extendsFrom(koraBom)
    annotationProcessor.extendsFrom(koraBom)
}

dependencies {
    koraBom platform("ru.tinkoff.kora:kora-parent:$koraVersion")
    annotationProcessor "ru.tinkoff.kora:annotation-processors"

    implementation "ru.tinkoff.kora:grpc-server"
    implementation "io.grpc:grpc-protobuf:1.58.0"
    implementation "javax.annotation:javax.annotation-api:1.3.2"

    implementation "ru.tinkoff.kora:logging-logback"
    implementation "ru.tinkoff.kora:config-hocon"

    testImplementation "ru.tinkoff.kora:test-junit5"
    testImplementation "org.testcontainers:junit-jupiter:1.17.6"
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:3.24.4" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:1.58.0" }
    }

    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

//noinspection GroovyAssignabilityCheck
run {
    environment([
            "GRPC_PORT"                : "$grpcPort",
            "METRIC_COLLECTOR_ENDPOINT": "$metricCollectorEndpoint",
    ])
}

test.dependsOn tasks.generateProto
test.dependsOn tasks.shadowJar

test {
    jvmArgs += [
            "-XX:+TieredCompilation",
            "-XX:TieredStopAtLevel=1",
    ]

    environment([
            "": ""
    ])

    useJUnitPlatform()
    testLogging {
        showStandardStreams(true)
        events("passed", "skipped", "failed")
        exceptionFormat("full")
    }

    jacoco {
        excludes += ["**/Application*"]
    }

    reports {
        html.required = false
        junitXml.required = false
    }
}

sourceSets {
    main {
        java {
            srcDirs "build/generated/source/proto/main/grpc"
            srcDirs "build/generated/source/proto/main/java"
        }
    }
}

jar.enabled = true
shadowJar {
    mergeServiceFiles()
    manifest {
        attributes "Main-Class": mainClassName
        attributes "Implementation-Version": koraVersion
        attributes "Build-Time": java.time.OffsetDateTime.now()
    }
}

artifacts {
    archives shadowJar
}

compileJava {
    options.encoding("UTF-8")
    options.incremental(true)
    options.fork = true
}

check.dependsOn jacocoTestReport
jacocoTestReport {
    reports {
        xml.required = true
        html.outputLocation = layout.buildDirectory.dir("jacocoHtml")
    }
}

javadoc {
    options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption("html5", true)
    }
}